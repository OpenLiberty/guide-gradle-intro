// tag::war[]
apply plugin: 'war'
// end::war[]
// tag::liberty[]
apply plugin: 'liberty'
// end::liberty[]

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

// configure liberty-gradle-plugin 
// tag::buildscript[]
buildscript {
    repositories {
        // tag::buildscriptmaven[]
        mavenCentral()
        // end::buildscriptmaven[]
    }
    dependencies {
        // tag::liberty-dependency[]
        classpath 'io.openliberty.tools:liberty-gradle-plugin:3.0'
        // end::liberty-dependency[]
    }
}
// end::buildscript[]

// tag::repositories[]
repositories {
    // tag::maven[]
    mavenCentral()
    // end::maven[]
}
// end::repositories[]

// tag::dependencies[]
dependencies {
    // provided dependencies
    // tag::providedcompile[]
    providedCompile 'jakarta.platform:jakarta.jakartaee-api:8.0.0'
    providedCompile 'org.eclipse.microprofile:microprofile:3.2'
    // end::providedcompile[]

    // test dependencies
    // tag::testimplementation[]
    // tag::junit[]
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.6.0'
    // end::junit[]
    // tag::commons[]
    testImplementation 'commons-httpclient:commons-httpclient:3.1'
    // end::commons[]
    // end::testimplementation[]
}
// end::dependencies[]

// tag::ext[]
ext  {
    liberty.server.var.'default.http.port' = '9080'
    liberty.server.var.'default.https.port' = '9443'
    liberty.server.var.'app.context.root' = project.name
}
// end::ext[]

// tag::openbrowser[]
task openBrowser {
    description = 'Open browser to http://localhost:8080/${warContext}'
    doLast {
        String port = liberty.server.var.'default.http.port'
        String context = liberty.server.var.'app.context.root'
        String URL = "http://localhost:" + port + "/" + context + "/" + "servlet"
        java.awt.Desktop.desktop.browse URL.toURI()
        java.awt.Desktop.desktop.browse file("$buildDir/reports/tests/test/index.html").toURI()
    }
}
// end::openbrowser[]

// tag::test[]
test {
    // tag::junit[]
    useJUnitPlatform()
    // end::junit[]
    testLogging {
        events 'passed', 'skipped', 'failed', 'standardOut'
        exceptionFormat 'full'
    }
    // tag::systemproperty[]
    // tag::httpport[]
    systemProperty 'http.port', liberty.server.var.'default.http.port'
    // end::httpport[]
    // tag::contextroot[]
    systemProperty 'context.root',  liberty.server.var.'app.context.root'
    // tag::contextroot[]
    // end::systemproperty[]
}
// end::test[]

// tag::depends[]
test.dependsOn 'libertyStart'
test.finalizedBy(openBrowser)
clean.dependsOn 'libertyStop'
// end::depends[]